
<html lang="en">
<head>
	<link rel="stylesheet" href="style.css">
	<link rel="icon" href="favicon.ico" type="image/vnd.microsoft.icon">
	
	<script type="text/javascript" src="./gl-matrix.js"></script>
	<script type="text/javascript" src="./menu.js"></script>
	<script type="text/javascript" src="./foo.js"></script>
   
<script id="shader-debugvs" type="x-shader/x-vertex">
	precision mediump float;
	attribute vec2 point;
	varying vec2 coord;

	void main(void)
	{
		coord = vec2(0.5,0.5) + point/2.;
		gl_Position = vec4(point,1,1);
	}
</script>

<script id="shader-debugfs" type="x-shader/x-fragment">
	precision mediump float;
	varying vec2 coord;
	uniform sampler2D tex;

	void main(void) {
		gl_FragColor = vec4(   (texture2D(tex, coord).rgb)  , 1);
		//gl_FragColor = vec4(coord,0,1);
	}
</script>

	<script id="shader-fs-depthtocolor" type="x-shader/x-fragment">
		precision highp float;
		varying vec4 pos;
		void main(void)
		{
			float val = 256.*256.*256.*clamp(gl_FragCoord.z, 0.0, 0.99999999);

			float r = floor(val / (256.*256.));
			val -= r*256.*256.;

			float g = floor(val / 256.);
			val -= g*256.;

			float b = floor(val);
			
			gl_FragColor = vec4(vec3(float(r)/255.,float(g)/255.,float(b)/255.),1);
			//gl_FragColor = vec4(vec3(gl_FragCoord.z,0,0),1);
		}
	</script>

	<script id="shader-fs" type="x-shader/x-fragment">
		// const bool doShadow; // gets prepended in the javascript
		// const bool haveDepthTextureExtension; // same
		precision mediump float;
		varying vec3 color;
		varying vec3 normal;
		varying vec4 pos;
		varying vec4 light_gl_Position;
		uniform sampler2D depth_map;

		//const vec3 light_pos = vec3(0,0,-20) -  vec3(0.25,0,-7.458);
		const vec3 light_pos = vec3(0,0,-20) - vec3(8.04,0,-10.96);

		void main(void) {
			vec3 light_dir = normalize(light_pos - pos.xyz/pos.w);
			vec3 n = normalize(normal);
			vec3 v = normalize(light_pos - pos.xyz/pos.w);

			vec3 r = 2.*dot(light_dir, n)*n - light_dir;




			vec3 amb = 0.1 * color;
			vec3 dif = max(0.,dot(light_dir, n)) * color;
			vec3 spe = 1. * pow(max(0.,dot(v, r)), 30.) * vec3(1,1,1);

			dif=max(dif,vec3(0));
			spe=max(spe,vec3(0));


			if (doShadow)
			{
				vec3 light_glPos_dehom = light_gl_Position.xyz / light_gl_Position.w;

				vec2 texcoord = light_glPos_dehom.xy / 2. + vec2(0.5,0.5);
				float l_dist = light_glPos_dehom.z;
				
				float l_depth;
				if (haveDepthTextureExtension) // no rgb unpacking hack needed
					l_depth = -1.+2.*texture2D(depth_map, texcoord).r;
				else // need to do rgb unpacking
					l_depth = -1.+2.* dot(vec3(1.,1./255.,1./255./256.), texture2D(depth_map, texcoord).rgb);

				float bias = 0.01 * tan(acos(dot(n,light_dir)));
				bias = clamp(bias,0.0,0.01);

				if (l_depth < l_dist-bias)
				{
					dif=dif*0.3;
					spe=vec3(0);
				}
			}
			
			
			gl_FragColor = vec4(amb+dif+spe,1);
		}
	</script>

<script id="shader-vs" type="x-shader/x-vertex">
precision mediump float;
  attribute vec3 aVertexPosition;
  attribute vec3 aVertexPosition2; // needed to emulate the missing
  attribute vec3 aVertexPosition3; // geometry shader
  attribute vec3 aVertexLevels;
  attribute vec3 aVertexColor;
  attribute vec4 aFaceRotation;

  uniform mat4 uMVMatrix;
  uniform mat4 uLightMVPMatrix;
  uniform mat4 uPMatrix;
  uniform float spike;
  uniform float spikeparam1;
  uniform float spikeparam2;
  uniform int colormode;
  uniform float explosion_time;
  uniform float spawn_time;
  
  varying vec3 color;
  varying vec3 normal;
  varying vec4 pos;
  varying vec4 light_gl_Position; // in the shadowing pass, this is unused. in the draw pass, this is set to what gl_Position would be in the shadow pass for that fragment
  
  
mat4 rotationMatrix(vec3 axis, float angle)
{
	axis = normalize(axis);
	float s = sin(angle);
	float c = cos(angle);
	float oc = 1.0 - c;

	return mat4(oc * axis.x * axis.x + c,           oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
			oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           oc * axis.y * axis.z - axis.x * s,  0.0,
			oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           0.0,
			0.0,                                0.0,                                0.0,                                1.0);
}

  void main(void) {

	vec3 levels = vec3(1,1,1) + pow(4., 2.-spikeparam1*2.)* 0.025*(0.3+spike) * pow(aVertexLevels, vec3(spikeparam1*2.+1.));
	const float spawn_duration = .5;
	float scale = ( 1.-cos( clamp(spawn_time,0.,spawn_duration) / spawn_duration * 3.141592 )) / 2.;
  

  	if (colormode==0)
    		color=aVertexColor;
	else
	{
		float color_level;
		if (colormode==1) color_level = aVertexLevels.x;
		if (colormode==2) color_level = min(min(aVertexLevels.x, aVertexLevels.y), aVertexLevels.z);
		if (colormode==3) color_level = (aVertexLevels.x + aVertexLevels.y + aVertexLevels.z) / 3. -1. ;
		if (color_level < 1.)
			color = vec3(1,0,0);
		else if (color_level < 2.)
			color = vec3(0,1,0);
		else if (color_level < 3.)
			color = vec3(0,0,1);
		else if (color_level < 4.)
			color = vec3(0,1,1);
		else if (color_level < 5.)
			color = vec3(1,1,0);
		else
			color = vec3(1,0,1);
	}
	
	vec4 mpos1 = vec4(scale*levels.x*aVertexPosition, 1);
	vec4 mpos2 = vec4(scale*levels.y*aVertexPosition2, 1);
	vec4 mpos3 = vec4(scale*levels.z*aVertexPosition3, 1);
	vec3 mmid = (mpos1.xyz + mpos2.xyz + mpos3.xyz) / 3.;

	float explode_radius = 3.* (1. - 1./exp(1.3*explosion_time)) + 1.;
	float particle_size = 1./exp(0.2*explosion_time*explosion_time*explosion_time);
	//float explode_radius = max(1., sin(explosion_time)+1.);
	
	const float rot_accel_duration = 0.3;
	float rot_t;
	if (explosion_time < rot_accel_duration)
		rot_t = explosion_time*explosion_time/rot_accel_duration/2.;
	else
		rot_t = explosion_time - rot_accel_duration/2.;

	mat4 rotmat = rotationMatrix(aFaceRotation.xyz, aFaceRotation.w*rot_t); 

	vec3 newmmid = explode_radius*mmid;
	mpos1 = vec4(vec3(particle_size),1) * rotmat * (mpos1-vec4(mmid,0))  +  (vec4(newmmid,0));
	mpos2 = vec4(vec3(particle_size),1) * rotmat * (mpos2-vec4(mmid,0))  +  (vec4(newmmid,0));
	mpos3 = vec4(vec3(particle_size),1) * rotmat * (mpos3-vec4(mmid,0))  +  (vec4(newmmid,0));


	pos = uMVMatrix      * mpos1;
	vec4 pos2= uMVMatrix * mpos2;
	vec4 pos3= uMVMatrix * mpos3;

	vec3 mid = (pos + pos2 + pos3).xyz / 3.;
	
	light_gl_Position = uLightMVPMatrix * mpos1;


//normal=normalize(mat3(uMVMatrix)*aVertexNormal);
    normal = normalize(  normalize( cross( vec3(pos-pos2), vec3(pos-pos3) ) )   +   2.*(-0.5+spikeparam2)* normalize(pos.xyz-mid) );
    
    //gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
    gl_Position = uPMatrix * pos;
  }
</script>
</head>



<body onload="start()">
<canvas id="glcanvas"></canvas>
<button type="button" id="menu_button" onclick="show_menu({clientX:0.0, clientY:0.0})">☰</button>
<div id="menu">
	<div class="menu_entry">
		<label for="rendertype_radio">Rendertype: </label>
		<form id="rendertype_radio">
			<input type="radio" id="r0" name="rendertype" value="0" onclick="igitt(this)" checked="checked"/>
			<label for="r0">0</label>
			<input type="radio" id="r1" name="rendertype" value="1" onclick="igitt(this)" />
			<label for="r1">1</label>
			<input type="radio" id="r2" name="rendertype" value="2" onclick="igitt(this)" />
			<label for="r2">2</label>
			<input type="radio" id="r3" name="rendertype" value="3" onclick="igitt(this)" />
			<label for="r3">3</label>
		</form>
	</div>
	<div class="menu_entry">
		<label for="x_slider">X: </label>
		<input id="x_slider" type="range" min="0" max="1.0" step="0.05" oninput="handle_slider(this)"/>
		<span id="x_slider_value"/>
	</div>
	<div class="menu_entry">
		<label for="y_slider">Y: </label>
		<input id="y_slider" type="range" min="0" max="1.0" step="0.05" oninput="handle_slider(this)"/>
		<span id="y_slider_value"/>
	</div>
	<div class="menu_entry">
		<label for="shadow_check">Shadows: </label>
		<form id="shadow_check">
			<input type="checkbox" id="shadowbox" name="shadow" value="0" onclick="handle_shadow(this)" checked="checked"/>
		</form>
	</div>
	<div class="menu_entry">
		<a href="https://github.com/Windfisch/stachelkugel">Source @ GitHub</a>
	</div>
</div>
</body>
